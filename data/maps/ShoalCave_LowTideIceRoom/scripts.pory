raw`ShoalCave_LowTideIceRoom_MapScripts::
	.byte 0

`

script ShoalCave_LowTideIceRoom_EventScript_Sylvia{
    msgbox(ShoalCave_LowTideIceRoom_Text_Sylvia, MSGBOX_NPC)
    end
}

script ShoalCave_LowTideIceRoom_EventScript_Nate{
    trainerbattle_single(TRAINER_NATE_SHOAL_CAVE, ShoalCave_LowTideIceRoom_Text_NateIntro, ShoalCave_LowTideIceRoom_Text_NateDefeat)
    msgbox(ShoalCave_LowTideIceRoom_Text_NatePostBattle, MSGBOX_AUTOCLOSE)
    waitmessage
	end
}

script ShoalCave_LowTideIceRoom_EventScript_Blake{
    trainerbattle_single(TRAINER_BLAKE_SHOAL_CAVE, ShoalCave_LowTideIceRoom_Text_BlakeIntro, ShoalCave_LowTideIceRoom_Text_BlakeDefeat)
    msgbox(ShoalCave_LowTideIceRoom_Text_BlakePostBattle, MSGBOX_AUTOCLOSE)
    waitmessage
	end
}

script ShoalCave_LowTideIceRoom_Trigger_Aria{
    lock
    playse(SE_M_DOUBLE_TEAM)
    delay(20)
    msgbox("They're swinging a sword.\n
    It looks dangerous to approach them\l
    from the front.", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkRight)
    waitmovement(0)
    release
    end
}

script ShoalCave_LowTideIceRoom_EventScript_Aria{
    lock
    getobjectfacingdirection(OBJ_EVENT_ID_PLAYER, VAR_TEMP_1)
    playbgm(MUS_NONE, FALSE)
    delay(20)
    playse(SE_M_DOUBLE_TEAM)
    msgbox("ARGH!", MSGBOX_AUTOCLOSE)
    waitmessage
    playse(SE_M_DOUBLE_TEAM)
    msgbox("AAAARGH!", MSGBOX_AUTOCLOSE)
    waitmessage
    playse(SE_PIN)
    applymovement(3, Common_Movement_ExclamationMark)
    waitmovement(3)
    applymovement(3, Common_Movement_FacePlayer)
    waitmovement(3)
    delay(40)
    playse(SE_M_YAWN)
    call(ShoalCave_LowTideIceRoom_EventScript_AriaPosition1)
    waitmovement(3)
    msgbox("AH!", MSGBOX_AUTOCLOSE)
    waitmessage
    fadedefaultbgm
    call(ShoalCave_LowTideIceRoom_EventScript_AriaPosition2)
    waitmovement(3)
    msgbox(ShoalCave_LowTideIceRoom_Text_Aria1, MSGBOX_AUTOCLOSE)
    waitmessage
    call(ShoalCave_LowTideIceRoom_EventScript_AriaLeave)
    removeobject(3)
    fadescreen(FADE_TO_BLACK)
    removeobject(4)
    removeobject(5)
    removeobject(6)
    fadescreenspeed(FADE_FROM_BLACK, 2)
    setflag(FLAG_HIDE_ARIA_SHOAL_CAVE)
    setvar(VAR_MOMO_ARIA_STATE, 1)
    setflag(FLAG_HIDE_MISA_LILYCOVE_DEPT_5F) // if player misses Misa in Lilycove
    setvar(VAR_MISA_STATE, 8)
    setflag(FLAG_HIDE_EMO_LILYCOVE) // if player misses cutscene in Lilycove
    release
    end
}

script ShoalCave_LowTideIceRoom_EventScript_AriaPosition1{
    if(var(VAR_TEMP_1) == DIR_SOUTH){
        applymovement(3, ShoalCave_LowTideIceRoom_Movement_WalkBackDown)
    }
    elif(var(VAR_TEMP_1) == DIR_WEST){
        applymovement(3, ShoalCave_LowTideIceRoom_Movement_WalkBackLeft)
    }
    else{
        applymovement(3, ShoalCave_LowTideIceRoom_Movement_WalkBackRight)
    }
    return
}

script ShoalCave_LowTideIceRoom_EventScript_AriaPosition2{
    if(var(VAR_TEMP_1) == DIR_SOUTH){
        applymovement(3, Common_Movement_WalkUp)
        waitmovement(3)
        applymovement(3, Common_Movement_FaceUp)
    }
    elif(var(VAR_TEMP_1) == DIR_WEST){
        applymovement(3, Common_Movement_WalkRight)
        waitmovement(3)
        applymovement(3, Common_Movement_FaceRight)
    }
    else{
        applymovement(3, Common_Movement_WalkLeft)
        waitmovement(3)
        applymovement(3, Common_Movement_FaceLeft)
    }
    return
}

script ShoalCave_LowTideIceRoom_EventScript_AriaLeave{
    if(var(VAR_TEMP_1) == DIR_SOUTH){
        applymovement(3, ShoalCave_LowTideIceRoom_Movement_AriaLeaveUp)
        delay(30)
        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceUp)
        waitmovement(3)
    }
    elif(var(VAR_TEMP_1) == DIR_WEST){
        applymovement(3, ShoalCave_LowTideIceRoom_Movement_AriaLeaveLeft)
        delay(30)
        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceUp)
        waitmovement(3)
    }
    else{
        applymovement(3, ShoalCave_LowTideIceRoom_Movement_AriaLeaveRight)
        delay(30)
        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceUp)
        waitmovement(3)
    }
    return
}

// unused
script ShoalCave_LowTideIceRoom_EventScript_AriaNorth{
    lock
    playbgm(MUS_NONE, FALSE)
    delay(20)
    playse(SE_PIN)
    applymovement(3, Common_Movement_ExclamationMark)
    waitmovement(3)
    delay(10)
    playse(SE_M_YAWN)
    msgbox("AH!", MSGBOX_AUTOCLOSE)
    applymovement(3, ShoalCave_LowTideIceRoom_Movement_WalkBackUp)
    waitmovement(3)
    waitmessage
    fadedefaultbgm
    msgbox(ShoalCave_LowTideIceRoom_Text_Aria1, MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(3, Common_Movement_WalkDown)
    waitmovement(3)
    applymovement(3, ShoalCave_LowTideIceRoom_Movement_AriaLeaveNorth)
    waitmovement(3)
    removeobject(3)
    fadescreen(FADE_TO_BLACK)
    removeobject(4)
    removeobject(5)
    removeobject(6)
    fadescreenspeed(FADE_FROM_BLACK, 3)
    setflag(FLAG_HIDE_ARIA_SHOAL_CAVE)
    setflag(FLAG_HIDE_MISA_LILYCOVE_DEPT_5F) // if player misses Misa in Lilycove
    setvar(VAR_MISA_STATE, 8)
    setflag(FLAG_HIDE_EMO_LILYCOVE) // if player misses cutscene in Lilycove
    release
    end
} 
// unused

text ShoalCave_LowTideIceRoom_Text_Sylvia{
    "If you're gonna try to get the\n
    commander's attention, don't sneak\l
    up on them!\p
    They scare easily."
}

text ShoalCave_LowTideIceRoom_Text_BlakeIntro{
    "For a knight, offense is the best\n
    defense!"
}

text ShoalCave_LowTideIceRoom_Text_BlakeDefeat{
    "H-How did you withstand my\n
    relentless offense…"
}

text ShoalCave_LowTideIceRoom_Text_BlakePostBattle{
    "People think our commander is larping,\n
    but the armor is real."
}

text ShoalCave_LowTideIceRoom_Text_NateIntro{
    "The greatest'th offense'th be a great\n
    defense!\p
    Dost thoust agree'th?"
}

text ShoalCave_LowTideIceRoom_Text_NateDefeat{
    "Thoust'th broketh thru'th thine\p
    defenses'th'snt'nt.."
}

text ShoalCave_LowTideIceRoom_Text_NatePostBattle{
    "I surmise thoust seeketh to joineth\n
    the PROTECTORS? If'th so, thou must\l
    speaketh to thine commander."
}

text ShoalCave_LowTideIceRoom_Text_Aria1{
    "You scared me!\n 
    Who are you?\p
    … … … …\n
    Oh. A challenger?\p
    I'm ARIA. LEADER of the MOSSDEEP\n
    POKéMON GYM and PROTECTOR of the\l
    SACRED BLADE.\p
    ARIA: I can stop my training here.\n
    Me and my knights shall await you\l
    at the GYM."
}

movement ShoalCave_LowTideIceRoom_Movement_WalkBackDown{
    lock_facing_direction
    walk_fast_down * 1
    unlock_facing_direction
}

movement ShoalCave_LowTideIceRoom_Movement_WalkBackLeft{
    lock_facing_direction
    walk_fast_left * 1
    unlock_facing_direction
}

movement ShoalCave_LowTideIceRoom_Movement_WalkBackRight{
    lock_facing_direction
    walk_fast_right * 1
    unlock_facing_direction
}

movement ShoalCave_LowTideIceRoom_Movement_AriaLeaveUp{
    walk_left
    walk_up * 4
    walk_left * 2
    walk_up * 4
}

movement ShoalCave_LowTideIceRoom_Movement_AriaLeaveLeft{
    walk_up * 4
    walk_left * 3
    walk_up * 4   
}

movement ShoalCave_LowTideIceRoom_Movement_AriaLeaveRight{
    walk_up * 2
    walk_left 
    walk_up * 2
    walk_left * 2
    walk_up * 4
}

movement ShoalCave_LowTideIceRoom_Movement_WalkBackUp{
    lock_facing_direction
    walk_fast_up * 2
    unlock_facing_direction
}

movement ShoalCave_LowTideIceRoom_Movement_AriaLeaveNorth{
    walk_left
    walk_up * 3
    walk_left * 2
    walk_up * 4
}
