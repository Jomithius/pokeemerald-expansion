mapscripts NewMoonIsland_DarkraiLair_MapScripts {
    MAP_SCRIPT_ON_LOAD{
        setflag(FLAG_TEMP_E)
        end
    }
    MAP_SCRIPT_ON_RESUME: NewMoonIsland_DarkraiLair_TryRemoveDarkrai
}

script NewMoonIsland_DarkraiLair_TryRemoveDarkrai{
    specialvar(VAR_RESULT, GetBattleOutcome)
    goto_if_eq(VAR_RESULT, B_OUTCOME_RAN, Common_EventScript_NopReturn)
    goto_if_eq(VAR_RESULT, B_OUTCOME_PLAYER_TELEPORTED, Common_EventScript_NopReturn)
    goto_if_eq(VAR_RESULT, B_OUTCOME_LOST, Common_EventScript_NopReturn)
    removeobject(VAR_LAST_TALKED)
    end
}

script NewMoonIsland_DarkraiLair_EventScript_Darkrai{
    lock
	faceplayer
    playmoncry(SPECIES_DARKRAI, CRY_MODE_ENCOUNTER)
    delay(40)
    waitmoncry
    setwildbattle(SPECIES_DARKRAI, 75, ITEM_COVERT_CLOAK)
    special(BattleSetup_StartLegendaryBattle)
    waitstate
    specialvar(VAR_RESULT, GetBattleOutcome)
    if(var(VAR_RESULT) == B_OUTCOME_RAN || var(VAR_RESULT) == B_OUTCOME_PLAYER_TELEPORTED){
		goto(Common_EventScript_NopReturn)
    }
    msgbox(NewMoonIsland_DarkraiLair_Text_PostDarkrai, MSGBOX_AUTOCLOSE)
    waitmessage
    fadescreenspeed(FADE_TO_BLACK, 2)
    delay(60)
    fadescreenspeed(FADE_FROM_BLACK, 3)
    delay(90)
    fadescreenspeed(FADE_TO_BLACK, 2)
    delay(60)
    fadescreenspeed(FADE_FROM_BLACK, 3)
    delay(90)
    setflag(FLAG_REMOVE_WARP_FADE)
    call_if_eq(VAR_RAT_CAVE_STATE, 8, NewMoonIsland_DarkraiLair_TryUpdateRatCaveState)
    setvar(VAR_NEWMOON_ISLAND_STATE, 5)
    fadescreenspeed(FADE_TO_BLACK, 6)
    delay(90)
    warpsilent(MAP_THE_UNDER_HOUSE, 11, 2)
    waitstate
	release
	end
}

// for "good" ratduardo ending
script NewMoonIsland_DarkraiLair_TryUpdateRatCaveState{
    clearflag(FLAG_HIDE_RATDUARDO_THE_UNDER_HOUSE)
    setvar(VAR_RAT_CAVE_STATE, 13)
    return
}

text NewMoonIsland_DarkraiLair_Text_PostDarkrai{
    "…A voice called out from somehere…\p
    ???: DARKRAI…\n
    Your power is strong.\p
    Though you don't will it, you make\n
    people and POKEMON around you see\l
    terrible nightmares…\p
    That is why you came here…\n
    NEWMOON ISLAND…\p
    There should be none that can be\n
    drawn into your nightmares…\p
    If anyone were to fall into a nightmare,\n
    FULLMOON ISLAND would be their only…\p
    …\n
    …"
}
