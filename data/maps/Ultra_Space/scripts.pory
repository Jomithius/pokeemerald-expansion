mapscripts Ultra_Space_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_ULTRA_SPACE_STATE, 1: Ultra_Space_EventScript_ArrivedInUltraSpace 
    ]
    MAP_SCRIPT_ON_LOAD: Ultra_Space_UpdatePCMetatiles
    MAP_SCRIPT_ON_RESUME: Ultra_Space_UpdateUltraBeasts
}

script Ultra_Space_UpdatePCMetatiles{
    call_if_set(FLAG_DEFEATED_BLACEPHALON, Ultra_Space_EventScript_AddPC)
    end
}

script Ultra_Space_UpdateUltraBeasts{
    call_if_set(FLAG_DEFEATED_BLACEPHALON, Ultra_Space_RemoveBlacephalon)
    call_if_ge(VAR_ULTRA_SPACE_STATE, 3, UltraSpace_TryRemoveUltraBeast)
    end
}

script Ultra_Space_RemoveBlacephalon{
    removeobject(4)
    return
}

script UltraSpace_TryRemoveUltraBeast{
    specialvar(VAR_RESULT, GetBattleOutcome)
    goto_if_eq(VAR_RESULT, B_OUTCOME_RAN, Common_EventScript_NopReturn)
    goto_if_eq(VAR_RESULT, B_OUTCOME_PLAYER_TELEPORTED, Common_EventScript_NopReturn)
    goto_if_eq(VAR_RESULT, B_OUTCOME_LOST, Common_EventScript_NopReturn)
    removeobject(VAR_LAST_TALKED)
    return
}

script Ultra_Space_EventScript_ArrivedInUltraSpace{
    lock
    setflag(FLAG_VISITED_ULTRA_SPACE)
    setrespawn(HEAL_LOCATION_ULTRA_SPACE)
    playse(SE_PIN)
    applymovement(2, Common_Movement_ExclamationMark)
    delay(10)
    applymovement(1, Common_Movement_ExclamationMark)
    delay(10)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
    waitmovement(0)
    applymovement(2, Ultra_Space_Movement_ManiacPanic)
    delay(10)
    applymovement(1, Ultra_Space_Movement_JamesLookAround)
    delay(10)
    applymovement(OBJ_EVENT_ID_PLAYER, Ultra_Space_Movement_PlayerLookAround)
    waitmovement(0)
    msgbox("MANIAC: No, no, no…!", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(2, Ultra_Space_Movement_Maniac1)
    waitmovement(2)
    applymovement(1, Common_Movement_WalkInPlaceRight)
    delay(16)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceUp)
    waitmovement(0)
    msgbox(Ultra_Space_Text_Maniac1, MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(1, Common_Movement_WalkInPlaceDown)
    waitmovement(1)
    applymovement(2, Common_Movement_WalkInPlaceLeft)
    waitmovement(2)
    msgbox("JAMES: Something…\p
        Something about this place feels…\n
        familiar.", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(1, Common_Movement_WalkInPlaceLeft)
    waitmovement(1)
    msgbox("I don't know why…", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(1, Common_Movement_WalkInPlaceRight)
    waitmovement(1)
    msgbox(Ultra_Space_Text_James1, MSGBOX_AUTOCLOSE)
    waitmessage
    playse(SE_BIKE_HOP)
    applymovement(2, Common_Movement_QuestionMark)
    waitmovement(2)
    waitse
    msgbox(Ultra_Space_Text_Maniac2, MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(2, Common_Movement_WalkInPlaceDown)
    waitmovement(2)
    msgbox("Either way, let us follow his intuition\n
        and keep moving.", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(2, Ultra_Space_Movement_ManiacToCentralArea)
    delay(10)
    applymovement(1, Ultra_Space_Movement_JamesToCentralArea)
    waitmovement(0)
    delay(20)
    removeobject(1)
    removeobject(2)
    setflag(FLAG_HIDE_JAMES_ULTRA_SPACE)
    setvar(VAR_ULTRA_SPACE_STATE, 2)
    release
    end
}

script Ultra_Space_EventScript_Blacephalon{
    lock
    msgbox("JAMES: {PLAYER}! Over here!", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(17, Common_Movement_WalkInPlaceUp)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Ultra_Space_Movement_PlayerToCentralArea)
    waitmovement(0)
    applymovement(16, Common_Movement_WalkInPlaceLeft)
    waitmovement(16)
    msgbox("JAMES: This area looks promising.\n
    He's investigating it now.", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(16, Common_Movement_WalkInPlaceUp)
    waitmovement(16)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceUp)
    waitmovement(16)
    applymovement(17, Ultra_Space_Movement_ManiacInvestigate)
    waitmovement(0)
    msgbox(Ultra_Space_Text_Maniac3, MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(17, Ultra_Space_Movement_ManiacToPortal)
    delay(16)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceLeft)
    delay(16)
    applymovement(16, Common_Movement_WalkInPlaceLeft)
    waitmovement(17)
    applymovement(16, Common_Movement_WalkInPlaceUp)
    waitmovement(16)
    playse(SE_PIN)
    applymovement(16, Common_Movement_ExclamationMark)
    waitmovement(16)
    waitse
    msgbox("JAMES: Look out!", MSGBOX_AUTOCLOSE)
    waitmessage
    playse(SE_PIN)
    applymovement(17, Common_Movement_ExclamationMark)
    waitmovement(17)
    waitse
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceUp)
    waitmovement(0)
    applymovement(4, Ultra_Space_Movement_Blacephalon)
    delay(16)
    applymovement(17, Ultra_Space_Movement_ManiacRunToPlayer)
    waitmovement(17)
    msgbox("MANIAC: W-What is that?!\n
        An undiscovered POKéMON?!\p
        Wait, it's attacking!", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(4, Ultra_Space_Movement_BlacephalonApproachesPlayer)
    waitmovement(4)
    setflag(FLAG_DEFEATED_BLACEPHALON)
    setwildbattle(SPECIES_BLACEPHALON, 75)
    dowildbattle
    applymovement(17, Ultra_Space_Movement_ManiacFacePlayer)
    waitmovement(17)
    msgbox(Ultra_Space_Text_Maniac4, MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(16, Common_Movement_WalkLeft)
    delay(16)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    msgbox("JAMES: Here you go.\n
        They should be effective at catching\l
        POKéMON from the ULTRA SPACE.", MSGBOX_AUTOCLOSE)
    giveitem(ITEM_BEAST_BALL, 50)
    waitmessage
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceUp)
    applymovement(16, Common_Movement_WalkInPlaceUp)
    waitmovement(0)
    applymovement(17, Common_Movement_FaceDown)
    waitmovement(17)
    msgbox(Ultra_Space_Text_Maniac5, MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(17, Ultra_Space_Movement_ManiacGoesToPermSpot)
    delay(60)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
    delay(10)
    applymovement(16, Common_Movement_WalkInPlaceDown)
    waitmovement(17)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceRight)
    delay(10)
    applymovement(16, Common_Movement_WalkInPlaceLeft)
    waitmovement(16)
    msgbox(Ultra_Space_Text_JamesGoingToExplore, MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(16,  Ultra_Space_Movement_JamesGoesToPermSpot)
    delay(32)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceUp)
    waitmovement(16)
    removeobject(16)
    removeobject(17)
    clearflag(FLAG_DEFEATED_NECROZMA)
    call(Ultra_Space_EventScript_AddPC)
    special(DrawWholeMapView)
    setvar(VAR_ULTRA_SPACE_STATE, 3)
    release
    end
}

script Ultra_Space_EventScript_James{
    lock
    faceplayer
    msgbox(Ultra_Space_Text_InvisiblePathHint, MSGBOX_NPC)
    waitmessage
    applymovement(19, Common_Movement_FaceOriginalDirection)
    waitmovement(19)
    release
    end
}

script Ultra_Space_EventScript_Maniac{
    lock
    faceplayer
    msgbox(Ultra_Space_Text_DoYouNeedMedicalAssistance, MSGBOX_AUTOCLOSE)
    waitmessage
    call(Common_EventScript_OutOfCenterPartyHeal)
    msgbox(Ultra_Space_Text_LetMeKnowIfYouMakeAnyDiscoveries, MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(18, Common_Movement_FaceOriginalDirection)
    waitmovement(18)
    release
    end
}

script Ultra_Space_EventScript_AddPC{
    setmetatile(26, 46, METATILE_DistortionWorld_Ultra_Space_PC_Top, TRUE)
    setmetatile(26, 47, METATILE_Building_PC_Off, TRUE)
    setmetatile(26, 48, METATILE_DistortionWorld_Ultra_Space_PC_Bottom, FALSE)
    return
}

script Ultra_Space_EventScript_Necrozma{
    lock
    playmoncry(SPECIES_NECROZMA, CRY_MODE_ENCOUNTER)
    waitmoncry
    setwildbattle(SPECIES_NECROZMA, 75, ITEM_ULTRANECROZIUM_Z)
    special(BattleSetup_StartLegendaryBattle)
    waitstate
    msgbox("{PLAYER}!", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
    waitmovement(0)
    addobject(20)
    addobject(21)
    clearflag(FLAG_HIDE_JAMES_ULTRA_SPACE)
    applymovement(20, Ultra_Space_Movement_JamesGoesToNecrozma)
    delay(16)
    applymovement(21, Ultra_Space_Movement_ManiacGoesToNecrozma)
    waitmovement(21)
    msgbox(Ultra_Space_Text_ThatWasCrazy, MSGBOX_AUTOCLOSE)
    waitmessage
    playse(SE_M_THUNDER_WAVE)
    fadescreen(FADE_TO_WHITE)
    fadescreen(FADE_FROM_WHITE)
    delay(20)
    playse(SE_M_THUNDER_WAVE)
    fadescreen(FADE_TO_WHITE)
    addobject(15)
    clearflag(FLAG_HIDE_COSMOG_ULTRA_SPACE)
    fadescreen(FADE_FROM_WHITE)
    delay(10)
    waitse
    playse(SE_PIN)
    applymovement(21, Common_Movement_ExclamationMark)
    delay(8)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
    delay(4)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceUp)
    delay(4)
    applymovement(20, Common_Movement_ExclamationMark)
    waitmovement(20)
    waitse
    msgbox("MANIAC: C-COSMOG?!", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(15, Ultra_Space_Movement_Cosmog)
    delay(20)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceLeft)
    delay(8)
    applymovement(20, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    msgbox(Ultra_Space_Text_Maniac6, MSGBOX_AUTOCLOSE)
    waitmessage
    playmoncry(SPECIES_COSMOG, CRY_MODE_ENCOUNTER)
    waitmoncry
    msgbox("Quite fortuitous, indeed!\n
        Now COSMOG, if you will.\l
        Please get us out of here!", MSGBOX_AUTOCLOSE)
    waitmessage
    closemessage
    playmoncry(SPECIES_COSMOG, CRY_MODE_ENCOUNTER)
    waitmoncry
    call(Ultra_Space_Shake)
    setflag(FLAG_RETURNED_FROM_ULTRA_SPACE)
    setflag(FLAG_HIDE_JAMES_ULTRA_SPACE)
    setobjectxyperm(15, 26, 48)
    warpwhitefade(MAP_ROUTE135_LAB, 7, 8)
    waitstate
    release
    end
}

script Ultra_Space_EventScript_Buzzwole{
    lock
    faceplayer
    playmoncry(SPECIES_BUZZWOLE, CRY_MODE_ENCOUNTER)
    waitmoncry
    setwildbattle(SPECIES_BUZZWOLE, 75)
    dowildbattle
    release
    end
}

script Ultra_Space_Shake{
    delay (30)
    setvar(VAR_0x8004, 2) // vertical pan
    setvar(VAR_0x8005, 6) // horizontal pan
    setvar(VAR_0x8006, 8) // num shakes
    setvar(VAR_0x8007, 4) // shake delay
    special(ShakeCamera)
    waitstate
    delay(60)
    playse(SE_M_THUNDER_WAVE)
    fadescreen(FADE_TO_WHITE)
    fadescreen(FADE_FROM_WHITE)
    delay(20)
    playse(SE_M_THUNDER_WAVE)
    fadescreen(FADE_TO_WHITE)
    fadescreen(FADE_FROM_WHITE)
    delay(10)
    waitse
    playse(SE_INTRO_BLAST)
    delay(20)
    return
}

text Ultra_Space_Text_DoYouNeedMedicalAssistance{
    "Ah, intern.\n
    You require medical assistance, yes?"
}

text Ultra_Space_Text_LetMeKnowIfYouMakeAnyDiscoveries{
    "Let me know if you find anything\n
    significant, okay?"
}

text Ultra_Space_Text_InvisiblePathHint{
    "Oh, {PLAYER}. Did you find anything\n
    interesting?\p
    In the distance I can see another island\n
    but I can't figure out how to get over\l
    there.\p
    I almost wanna just try and run there\n
    from here…"
}

text Ultra_Space_Text_Maniac1{
    "This wasn't supposed to happen…\n
    It appears we've been pulled into\l
    the rift.\p
    I'm not sure if we'll ever be able\n
    to return to our world…"
}

text Ultra_Space_Text_James1{
    "But I think if we head north we might\n
    find something."
}

text Ultra_Space_Text_Maniac2{
    "MANIAC: …Intruiging.\n
    Perhaps you had stumbled across a rift\l
    in the past…\p
    Although I've never heard reports of\n
    such an occurance happening to anyone."
}

text Ultra_Space_Text_Maniac3{
    "MANIAC: Hm… Intruiging.\n
    It appears as though these caves\l
    are a means of connection to the\l
    other floating islands…\l
    Perhaps we can find another rift\l
    through them…\p
    I must get a closer look!"
}

text Ultra_Space_Text_Maniac4{
    "MANIAC: T-That was close!\n
    But still! A new discovery!\p
    This will push my research forward at\n
    lightspeed!\p
    Now that we know there's undiscovered\n
    POKéMON here, we can test the new\l
    POKé BALL I designed…\p
    Assistant, please give some of them\n
    to the intern."
}

text Ultra_Space_Text_Maniac5{
    "MANIAC: Now, while that was a riveting\n
    experience, I'd rather be safe than\l
    sorry.\p
    I'll leave the exploration to you two\n
    while I try to analyze the area as much\l
    as I can with what I have on hand.\p
    Call for me if you find anything\n
    significant or require medical aid."
}

text Ultra_Space_Text_JamesGoingToExplore{
    "JAMES: I'm going to survey this central\n
    area and make sure the PROF. is safe.\p
    Maybe there's a clue on how to get\n
    out of here."
}

text Ultra_Space_Text_ThatWasCrazy{
    "JAMES: I saw you walk across the sky\n
    and went to get the PROF.\l
    That was crazy!\p
    MANIAC: Yes, maniacal indeed…\n
    Unfortunately, there's still no rift\l
    over here."
}

text Ultra_Space_Text_Maniac6{
    "MANIAC: You came for us?!\n
    R-RESPLENDENT!\p
    I had no idea it had the ability\n
    to traverse the ULTRA SPACE!\p
    COSMOG, are you able to take us\n
    back to the lab?"
}

movement Ultra_Space_Movement_ManiacPanic{
    walk_fast_right
    delay_8
    walk_fast_up
    delay_8
    walk_in_place_fast_left
    delay_8
    walk_in_place_fast_right
    delay_8
    walk_in_place_fast_up
    walk_down
}

movement Ultra_Space_Movement_Maniac1{
    walk_left * 2
    walk_in_place_down
}

movement Ultra_Space_Movement_JamesLookAround{
    walk_in_place_left
    delay_16
    walk_in_place_right
    delay_16
    walk_in_place_up
}

movement Ultra_Space_Movement_PlayerLookAround{
    walk_in_place_up
    delay_16
    walk_in_place_down
    delay_16
    walk_in_place_left
    delay_16
    walk_in_place_right
}

movement Ultra_Space_Movement_ManiacToCentralArea{
    walk_up * 4
    walk_left * 2
}

movement Ultra_Space_Movement_JamesToCentralArea{
    walk_right
    walk_up * 4
    walk_left
    walk_up * 2
}

movement Ultra_Space_Movement_PlayerToCentralArea{
    walk_up * 6
    walk_in_place_right
}

movement Ultra_Space_Movement_ManiacToPortal{
    walk_left * 4
}

movement Ultra_Space_Movement_ManiacRunToPlayer{
    walk_fast_down * 2
    walk_fast_right * 2
    walk_fast_down
    walk_in_place_fast_up
}

movement Ultra_Space_Movement_Blacephalon{
    walk_down * 2
    walk_right
    walk_down * 3
}

movement Ultra_Space_Movement_BlacephalonApproachesPlayer{
    walk_right * 3
    walk_down
}

movement Ultra_Space_Movement_ManiacFacePlayer{
    walk_up * 3
    walk_in_place_left
    delay_16
    walk_in_place_right
    delay_16
    walk_right * 2
    walk_in_place_down
}

movement Ultra_Space_Movement_ManiacGoesToPermSpot{
    walk_left * 2
    walk_down * 4
    walk_right
    walk_down * 4
}

movement Ultra_Space_Movement_JamesGoesToPermSpot{
    walk_up
    walk_left * 4
    walk_up * 4
    walk_left * 3
    walk_up * 2
}

movement Ultra_Space_Movement_JamesGoesToNecrozma{
    walk_up * 6
    walk_right
    walk_in_place_up
}

movement Ultra_Space_Movement_ManiacGoesToNecrozma{
    walk_up * 7
    walk_left
    walk_in_place_up
}

movement Ultra_Space_Movement_Cosmog{
    walk_left
    walk_down * 3
}

movement Ultra_Space_Movement_ManiacInvestigate{
    walk_up
    delay_8
    walk_right
    delay_8
    walk_in_place_left
    delay_2
    walk_in_place_right
    delay_2
    walk_left
    delay_16
    walk_down
}
