mapscripts NewMoonIsland_House2_MapScripts {
    MAP_SCRIPT_ON_LOAD: NewMoonIsland_House2_OnLoad
}

script NewMoonIsland_House2_OnLoad{
    setflag(FLAG_TEMP_E)
    call_if_set(FLAG_RECEIVED_GIMMIGHOUL_COINS_HOUSE_2, NewMoonIsland_House2_HideGimmighoulCoins)
    call_if_eq(VAR_NEWMOON_ISLAND_STATE, 4, NewMoonIsland_House2_HideTrapChest)
    end
}


script NewMoonIsland_House2_HideGimmighoulCoins{
    setmetatile(14, 2, METATILE_LegendOfZeldaHouse_FloorTileTop, FALSE)
    return
}

script NewMoonIsland_House2_HideTrapChest{
    setmetatile(13, 2, METATILE_LegendOfZeldaHouse_FloorTileTop, FALSE)
    return
}

script NewMoonIsland_House2_EventScript_TriggerTrap{
    lockall
    playbgm(MUS_NONE, TRUE)
    setweather(WEATHER_FOG_DIAGONAL)
    doweather
    special(WaitWeather)
    removeobject(9)
    goto_if_ge(VAR_RAT_CAVE_STATE 11, NewMoonIsland_House2_EventScript_SpawnChanneler)
    addobject(5)
    addobject(6)
    addobject(7)
    clearflag(FLAG_HIDE_RATDUARDO_NEWMOON_ISLAND)
    setvar(VAR_NEWMOON_ISLAND_STATE, 3)
    msgbox("{PLAYER}…\n
        You shouldn't have done that.", MSGBOX_AUTOCLOSE)
    waitmessage
    releaseall
    end
}

script NewMoonIsland_House2_EventScript_SpawnChanneler{
    lockall
    addobject(10)
    addobject(11)
    addobject(12)
    clearflag(FLAG_HIDE_CHANNELER_NEWMOON_ISLAND)
    msgbox("{PLAYER}…\n
        You shouldn't have done that.", MSGBOX_AUTOCLOSE)
    waitmessage
    releaseall
    end
}

script NewMoonIsland_House2_EventScript_GimmighoulCoin{
    lock
    msgbox("You check the treasure chest…\n
        There's a coin!", MSGBOX_AUTOCLOSE)
    waitmessage
    checkitemspace(ITEM_GIMMIGHOUL_COIN)
    if(var(VAR_RESULT) == TRUE){
        finditem(ITEM_GIMMIGHOUL_COIN)
        waitmessage
        closemessage
        fadescreen(FADE_TO_BLACK)
        call(NewMoonIsland_House2_HideGimmighoulCoins)
        special(DrawWholeMapView)
	    fadescreen(FADE_FROM_BLACK)
    }
    else{
        msgbox("You don't have enough room…", MSGBOX_AUTOCLOSE)
        waitmessage
    }
    release
    end
}

script NewMoonIsland_House2_EventScript_Ratduardo{
    lockall
    faceplayer
    showobjectat(VAR_LAST_TALKED, MAP_NEW_MOON_ISLAND_HOUSE2)
    delay(8)
    hideobjectat(VAR_LAST_TALKED, MAP_NEW_MOON_ISLAND_HOUSE2)
    delay(16)
    showobjectat(VAR_LAST_TALKED, MAP_NEW_MOON_ISLAND_HOUSE2)
    delay(24)
    hideobjectat(VAR_LAST_TALKED, MAP_NEW_MOON_ISLAND_HOUSE2)
    delay(30)
    showobjectat(VAR_LAST_TALKED, MAP_NEW_MOON_ISLAND_HOUSE2)
    msgbox("…", MSGBOX_AUTOCLOSE)
    waitmessage
    trainerbattle_no_intro(TRAINER_RATDUARDO_NEWMOON_ISLAND, NewMoonIsland_House2_Text_RatduardoDefeat)
    fadescreen(FADE_TO_BLACK)
    call(NewMoonIsland_House2_HideTrapChest)
    removeobject(5)
    removeobject(6)
    removeobject(7)
    special(DrawWholeMapView)
    resetweather
    doweather
    special(WaitWeather)
	fadescreen(FADE_FROM_BLACK)
    setvar(VAR_NEWMOON_ISLAND_STATE, 4)
    fadedefaultbgm
    releaseall
    end
}

script NewMoonIsland_House2_EventScript_Channeler{
    lockall
    faceplayer
    showobjectat(VAR_LAST_TALKED, MAP_NEW_MOON_ISLAND_HOUSE2)
    delay(8)
    hideobjectat(VAR_LAST_TALKED, MAP_NEW_MOON_ISLAND_HOUSE2)
    delay(16)
    showobjectat(VAR_LAST_TALKED, MAP_NEW_MOON_ISLAND_HOUSE2)
    delay(24)
    hideobjectat(VAR_LAST_TALKED, MAP_NEW_MOON_ISLAND_HOUSE2)
    delay(30)
    showobjectat(VAR_LAST_TALKED, MAP_NEW_MOON_ISLAND_HOUSE2)
    msgbox("…", MSGBOX_AUTOCLOSE)
    waitmessage
    trainerbattle_no_intro(TRAINER_CHANNELER_NEWMOON_ISLAND, NewMoonIsland_House2_Text_RatduardoDefeat)
    fadescreen(FADE_TO_BLACK)
    call(NewMoonIsland_House2_HideTrapChest)
    removeobject(10)
    removeobject(11)
    removeobject(12)
    special(DrawWholeMapView)
	fadescreen(FADE_FROM_BLACK)
    resetweather
    doweather
    special(WaitWeather)
    setvar(VAR_NEWMOON_ISLAND_STATE, 4)
    fadedefaultbgm
    releaseall
    end
}

text NewMoonIsland_House2_Text_RatduardoDefeat{
    "…\n
    There's no escape…"
}

script NewMoonIsland_House2_EventScript_Raticate{
    lock
    msgbox("Just because we're trapped here\n
        doesn't mean we can't be a family.", MSGBOX_NPC)
    applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
    waitmovement(0)
    release
    end
}

script NewMoonIsland_House2_EventScript_RaticateAlola{
    lock
    faceplayer
    msgbox("What, {PLAYER}?\n
        Never seen a rat family before?", MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES){
        msgbox("Guess you're just uncultured.", MSGBOX_AUTOCLOSE)
    }
    else{
        msgbox("Then come join us for dinner.", MSGBOX_AUTOCLOSE)
    }
    waitmessage
    applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
    waitmovement(0)
    release
    end
}

script NewMoonIsland_House2_EventScript_RattataAlola{
    lock
    faceplayer
    msgbox("My stummy hurt.", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
    waitmovement(0)
    release
    end
}

script NewMoonIsland_House2_EventScript_Rattata{
    lock
    faceplayer
    msgbox("How are you so calm with her standing\n
        right behind you?", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
    waitmovement(0)
    release
    end
}
