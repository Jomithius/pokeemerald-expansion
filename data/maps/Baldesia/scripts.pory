const LOCALID_MOMO = 22
const LOCALID_BENNIS = 2
const LOCALID_ASAK = 1
const LOCALID_PANINI = 3
const LOCALID_ZORUA = 4
const LOCALID_NINJABOY1 = 7
const LOCALID_NINJABOY2 = 19
const LOCALID_NINJABOY3 = 17
const LOCALID_NINJABOY4 = 18
const LOCALID_NINJABOY5 = 16
const LOCALID_NINJABOY6 = 20


mapscripts Baldesia_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: Baldesia_OnTransition
    MAP_SCRIPT_ON_RESUME: Baldesia_OnResume
}

script Baldesia_OnTransition{
    call_if_set(FLAG_HIDE_NINJA_BOY_1_BALDESIA, Baldesia_UpdateNinjaBoy1Location)
    call_if_set(FLAG_HIDE_NINJA_BOY_2_BALDESIA, Baldesia_UpdateNinjaBoy2Location)
    call_if_set(FLAG_HIDE_NINJA_BOY_3_BALDESIA, Baldesia_UpdateNinjaBoy3Location)
    call_if_set(FLAG_HIDE_NINJA_BOY_4_BALDESIA, Baldesia_UpdateNinjaBoy4Location)
    call_if_set(FLAG_HIDE_NINJA_BOY_5_BALDESIA, Baldesia_UpdateNinjaBoy5Location)
    call_if_set(FLAG_HIDE_NINJA_BOY_6_BALDESIA, Baldesia_UpdateNinjaBoy6Location)
    end
}

script Baldesia_OnResume{
    call_if_set(FLAG_HISUIAN_ZORUA, Baldesia_RemoveHisuianZorua)
    end
}

script Baldesia_RemoveHisuianZorua{
    removeobject(24)
    return
}

script Baldesia_UpdateNinjaBoy1Location{
    setobjectxyperm(LOCALID_NINJABOY1, 21, 24)
    setobjectmovementtype(LOCALID_NINJABOY1, MOVEMENT_TYPE_LOOK_AROUND)
    return
}

script Baldesia_UpdateNinjaBoy2Location{
    setobjectxyperm(LOCALID_NINJABOY2, 21, 23)
    setobjectmovementtype(LOCALID_NINJABOY2, MOVEMENT_TYPE_LOOK_AROUND)
    return
}

script Baldesia_UpdateNinjaBoy3Location{
    setobjectxyperm(LOCALID_NINJABOY3, 23, 23)
    setobjectmovementtype(LOCALID_NINJABOY3, MOVEMENT_TYPE_MOUNTAIN_DISGUISE)
    return
}

script Baldesia_UpdateNinjaBoy4Location{
    setobjectxyperm(LOCALID_NINJABOY4, 25, 27)
    setobjectmovementtype(LOCALID_NINJABOY4, MOVEMENT_TYPE_WANDER_AROUND)
    return
}

script Baldesia_UpdateNinjaBoy5Location{
    setobjectxyperm(LOCALID_NINJABOY5, 21, 26)
    setobjectmovementtype(LOCALID_NINJABOY5, MOVEMENT_TYPE_WALK_IN_PLACE_DOWN)
    return
}

script Baldesia_UpdateNinjaBoy6Location{
    setobjectxyperm(LOCALID_NINJABOY6, 23, 24)
    return
}

script Baldesia_EventScript_OldManMiniGame{
    lock
    faceplayer
    goto_if_eq(VAR_BALDESIA_MINI_GAME_STATE, 0 Baldesia_EventScript_StartMiniGame)
    goto_if_eq(VAR_BALDESIA_MINI_GAME_STATE, 1 Baldesia_EventScript_RemindMiniGame)
    goto_if_eq(VAR_BALDESIA_MINI_GAME_STATE, 2 Baldesia_EventScript_GiveResult)
    goto_if_eq(VAR_BALDESIA_MINI_GAME_STATE, 3 Baldesia_EventScript_GiveResult)
    goto_if_eq(VAR_BALDESIA_MINI_GAME_STATE, 4 Baldesia_EventScript_GiveResult)
    goto_if_eq(VAR_BALDESIA_MINI_GAME_STATE, 5 Baldesia_EventScript_GiveResult)
    goto_if_eq(VAR_BALDESIA_MINI_GAME_STATE, 6 Baldesia_EventScript_GiveResult)
    goto_if_eq(VAR_BALDESIA_MINI_GAME_STATE, 7, Baldesia_EventScript_GiveLeadersCrest)
    msgbox("That crest is demanding of respect\n
        in our culture.\p
        It proves you have the qualities to\n
        become a powerful leader.", MSGBOX_AUTOCLOSE)
    waitmessage
    release
    end
}

script Baldesia_EventScript_StartMiniGame{
    lock
    msgbox("Hm… I sense potential in you, young one.\n
        How about a little challenge?\p
        If you can find my 6 disciples that are\n
        hidden around the village, I'll give\l
        you a reward." MSGBOX_AUTOCLOSE)
    waitmessage
    clearflag(FLAG_BALDESIA_MINI_GAME_START)
    setvar(VAR_BALDESIA_MINI_GAME_STATE, 1)
    release
    end
}

script Baldesia_EventScript_RemindMiniGame{
    lock
    msgbox("If you can find my 6 disciples that are\n
        hidden outside around the village, I'll\l
        give you a reward.", MSGBOX_AUTOCLOSE)
    waitmessage
    release
    end
}

script Baldesia_EventScript_GiveResult{
    buffernumberstring(STR_VAR_1, VAR_BALDESIA_NINJA_BOY_COUNTER)
    msgbox("So far you've found {STR_VAR_1} out of\n
        6 disciples.\p
        Keep searching, young one.", MSGBOX_AUTOCLOSE)
    waitmessage
    release
    end
}

script Baldesia_EventScript_GiveLeadersCrest{
    lock
    msgbox("Hm… So you were able to find all\n
    of my disciples… Impressive.\p
    As promised, here's your reward." MSGBOX_AUTOCLOSE)
    waitmessage
    checkitemspace(ITEM_LEADERS_CREST)
    if(var(VAR_RESULT) == TRUE){
        giveitem(ITEM_LEADERS_CREST)
        waitmessage
        msgbox("That crest is demanding of respect\n
        in our culture.\p
        It proves you have the qualities to\n
        become a powerful leader.", MSGBOX_AUTOCLOSE)
        setvar(VAR_BALDESIA_MINI_GAME_STATE, 8)
    }
    else{
        msgbox("Hm… It seems you don't have enough room,\n
        young one.", MSGBOX_AUTOCLOSE)
    }
    waitmessage
    release
    end
}

script Baldesia_EventScript_NinjaBoy1{
    lock
    if(flag(FLAG_HIDE_NINJA_BOY_1_BALDESIA) == FALSE && var(VAR_BALDESIA_MINI_GAME_STATE) >= 1){
        delay(16)
        playse(SE_PIN)
        applymovement(LOCALID_NINJABOY1, Common_Movement_ExclamationMark)
        waitmovement(0)
        waitse
        faceplayer
        msgbox("Uh oh!\n 
            You found me!", MSGBOX_AUTOCLOSE)
        waitmessage
        fadescreen(FADE_TO_BLACK)
        playse(SE_FLEE)
        removeobject(VAR_LAST_TALKED)
        clearflag(FLAG_BALDESIA_MINI_GAME_START)
        call(Baldesia_UpdateNinjaBoy1Location)
        special(DrawWholeMapView)
        waitse
        fadescreen(FADE_FROM_BLACK)
        addvar(VAR_BALDESIA_NINJA_BOY_COUNTER, 1)
        addvar(VAR_BALDESIA_MINI_GAME_STATE, 1)
        setflag(FLAG_HIDE_NINJA_BOY_1_BALDESIA)
    }
    else{
        faceplayer
        msgbox("Baldy, baldy, over there!\n
        What's it like to have no hair?", MSGBOX_AUTOCLOSE)
        waitmessage
    }
    release
    end
}

script Baldesia_EventScript_NinjaBoy2{
    lock
    if(flag(FLAG_HIDE_NINJA_BOY_2_BALDESIA) == FALSE && var(VAR_BALDESIA_MINI_GAME_STATE) >= 1){
        delay(16)
        playse(SE_PIN)
        applymovement(LOCALID_NINJABOY2, Common_Movement_ExclamationMark)
        waitmovement(0)
        waitse
        faceplayer
        msgbox("Uh oh!\n 
            You found me!", MSGBOX_AUTOCLOSE)
        waitmessage
        fadescreen(FADE_TO_BLACK)
        removeobject(VAR_LAST_TALKED)
        clearflag(FLAG_BALDESIA_MINI_GAME_START)
        playse(SE_FLEE)
        call(Baldesia_UpdateNinjaBoy2Location)
        special(DrawWholeMapView)
        waitse
        fadescreen(FADE_FROM_BLACK)
        addvar(VAR_BALDESIA_NINJA_BOY_COUNTER, 1)
        addvar(VAR_BALDESIA_MINI_GAME_STATE, 1)
        setflag(FLAG_HIDE_NINJA_BOY_2_BALDESIA)
    }
    else{
        faceplayer
        msgbox("Is it hot, or is it cold?\n
            I don't know, 'cause I'm not bald!", MSGBOX_AUTOCLOSE)
        waitmessage
    }
    release
    end
}

script Baldesia_EventScript_NinjaBoy3{
    lock
    call(EventScript_RevealTrainer)
    if(flag(FLAG_HIDE_NINJA_BOY_3_BALDESIA) == FALSE && var(VAR_BALDESIA_MINI_GAME_STATE) >= 1){
        delay(16)
        playse(SE_PIN)
        applymovement(LOCALID_NINJABOY3, Common_Movement_ExclamationMark)
        waitmovement(0)
        waitse
        faceplayer
        msgbox("W-What?!\n 
            My disguise was perfect!", MSGBOX_AUTOCLOSE)
        waitmessage
        fadescreen(FADE_TO_BLACK)
        removeobject(VAR_LAST_TALKED)
        clearflag(FLAG_BALDESIA_MINI_GAME_START)
        playse(SE_FLEE)
        call(Baldesia_UpdateNinjaBoy3Location)
        special(DrawWholeMapView)
        waitse
        fadescreen(FADE_FROM_BLACK)
        addvar(VAR_BALDESIA_NINJA_BOY_COUNTER, 1)
        addvar(VAR_BALDESIA_MINI_GAME_STATE, 1)
        setflag(FLAG_HIDE_NINJA_BOY_3_BALDESIA)
    }
    else{
        faceplayer
        msgbox("I'm practicing my disguises!", MSGBOX_AUTOCLOSE)
        waitmessage
    }
    release
    end
}

script Baldesia_EventScript_NinjaBoy4{
    lock
    if(flag(FLAG_HIDE_NINJA_BOY_4_BALDESIA) == FALSE && var(VAR_BALDESIA_MINI_GAME_STATE) >= 1){
        delay(16)
        playse(SE_PIN)
        applymovement(LOCALID_NINJABOY4, Common_Movement_ExclamationMark)
        waitmovement(0)
        waitse
        faceplayer
        msgbox("Aw!\n 
            You found me!", MSGBOX_AUTOCLOSE)
        waitmessage
        fadescreen(FADE_TO_BLACK)
        removeobject(VAR_LAST_TALKED)
        clearflag(FLAG_BALDESIA_MINI_GAME_START)
        playse(SE_FLEE)
        call(Baldesia_UpdateNinjaBoy4Location)
        special(DrawWholeMapView)
        waitse
        fadescreen(FADE_FROM_BLACK)
        addvar(VAR_BALDESIA_NINJA_BOY_COUNTER, 1)
        addvar(VAR_BALDESIA_MINI_GAME_STATE, 1)
        setflag(FLAG_HIDE_NINJA_BOY_4_BALDESIA)
    }
    else{
        faceplayer
        msgbox("I thought hiding in plain sight was\n
            a good idea.", MSGBOX_AUTOCLOSE)
        waitmessage
    }
    release
    end
}

script Baldesia_EventScript_NinjaBoy5{
    lock
    if(flag(FLAG_HIDE_NINJA_BOY_5_BALDESIA) == FALSE && var(VAR_BALDESIA_MINI_GAME_STATE) >= 1){
        delay(16)
        playse(SE_PIN)
        applymovement(LOCALID_NINJABOY5, Common_Movement_ExclamationMark)
        waitmovement(0)
        waitse
        faceplayer
        msgbox("Dang!\n 
            You found me!", MSGBOX_AUTOCLOSE)
        waitmessage
        fadescreen(FADE_TO_BLACK)
        removeobject(VAR_LAST_TALKED)
        clearflag(FLAG_BALDESIA_MINI_GAME_START)
        playse(SE_FLEE)
        call(Baldesia_UpdateNinjaBoy5Location)
        special(DrawWholeMapView)
        waitse
        fadescreen(FADE_FROM_BLACK)
        addvar(VAR_BALDESIA_NINJA_BOY_COUNTER, 1)
        addvar(VAR_BALDESIA_MINI_GAME_STATE, 1)
        setflag(FLAG_HIDE_NINJA_BOY_5_BALDESIA)
    }
    else{
        faceplayer
        msgbox("I'm training to be a great ninja\n
        like MASTER BENNIS!\p
        It's not fair that only PANINI gets to\n
        be his disciple!", MSGBOX_AUTOCLOSE)
        waitmessage
    }
    applymovement(LOCALID_NINJABOY5, Common_Movement_FaceOriginalDirection)
    waitmovement(0)
    release
    end
}

script Baldesia_EventScript_NinjaBoy6{
    lock
    if(flag(FLAG_HIDE_NINJA_BOY_6_BALDESIA) == FALSE && var(VAR_BALDESIA_MINI_GAME_STATE) >= 1){
        delay(16)
        playse(SE_PIN)
        applymovement(LOCALID_NINJABOY6, Common_Movement_ExclamationMark)
        waitmovement(0)
        waitse
        faceplayer
        msgbox("…!", MSGBOX_AUTOCLOSE)
        waitmessage
        fadescreen(FADE_TO_BLACK)
        removeobject(VAR_LAST_TALKED)
        clearflag(FLAG_BALDESIA_MINI_GAME_START)
        playse(SE_FLEE)
        call(Baldesia_UpdateNinjaBoy6Location)
        special(DrawWholeMapView)
        waitse
        fadescreen(FADE_FROM_BLACK)
        addvar(VAR_BALDESIA_NINJA_BOY_COUNTER, 1)
        addvar(VAR_BALDESIA_MINI_GAME_STATE, 1)
        setflag(FLAG_HIDE_NINJA_BOY_6_BALDESIA)
    }
    else{
        faceplayer
        msgbox("…\n
        …\l
        …\p
        … I wanna grow up to be bald like my\n
        Auntie MOMO.", MSGBOX_AUTOCLOSE)
        waitmessage
    }
    release
    end
}

script Baldesia_EventScript_LittleGirl{
    msgbox("My mom said the ASAKURA family has\n
        a gene that makes the women bald!\p
        Hahaha!", MSGBOX_NPC)
    end
}

script Baldesia_EventScript_OldWoman{
    msgbox("There's a DOJO in the woods renowned\n
        for the strength of its members.\p
        I heard the Sensei there is looking\n
        for a successor.", MSGBOX_NPC)
    end
}

script Baldesia_EventScript_OldManBridge{
    lock
    faceplayer
    msgbox("I helped dig this moat about\n
        fifty years ago.\p
        Time really flies.", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
    waitmovement(0)
    end
}

script Baldesia_EventScript_KimonoLady1{
    msgbox("There's a new flavor of mochi that's\n
        becoming super popular in the village.\p
        The Chief and his wife seem to enjoy it\n
        the most.", MSGBOX_NPC)
    end
}

script Baldesia_EventScript_KimonoLady2{
    msgbox("I heard the Young Lady left the village\n
        because she met somebody.\p
        Oh, to be in love…", MSGBOX_NPC)
    end
} 

script Baldesia_EventScript_Ninja{
    msgbox("This land has been home to\n
        ninjas for many generations.", MSGBOX_NPC)
    end
} 

script Baldesia_EventScript_EntranceGuard{
    lock
    faceplayer
    msgbox("Don't get into any trouble.", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
    waitmovement(0)
    release
    end
} 

script Baldesia_EventScript_Sawk{
    lock
    faceplayer
    playmoncry(SPECIES_SAWK, CRY_MODE_ENCOUNTER)
    waitmoncry
    applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
    waitmovement(0)
    release
    end
}

script Baldesia_Entrance_TriggerLeft{
    lockall
    setvar(VAR_TEMP_B, 1)
    goto(Baldesia_EventScript_HisuianZoruaTrigger)
    end
}

script Baldesia_Entrance_TriggerRight{
    lockall
    setvar(VAR_TEMP_B, 2)
    goto(Baldesia_EventScript_HisuianZoruaTrigger)
    end
}

script Baldesia_EventScript_HisuianZoruaTrigger{
    lockall
    playse(SE_PIN)
    applymovement(11, Common_Movement_ExclamationMark)
    delay(8)
    applymovement(12, Common_Movement_ExclamationMark)
    waitmovement(12)
    waitse
    applymovement(11, Baldesa_Movement_BlockTrainerLeftGuard)
    delay(16)
    applymovement(12, Baldesa_Movement_BlockTrainerRightGuard)
    waitmovement(12)
    delay(10)
    msgbox("Halt!\n
        I've never seen you before.\p
        I'm sorry, but we can't just let\n
        you enter.", MSGBOX_AUTOCLOSE)
    waitmessage
    clearflag(FLAG_HIDE_BENNIS_BALDESIA)
    addobject(LOCALID_ASAK)
    applymovement(LOCALID_ASAK, Common_Movement_WalkDown4)
    waitmovement(LOCALID_ASAK)
    playse(SE_PIN)
    applymovement(LOCALID_ASAK, Common_Movement_ExclamationMark)
    waitmovement(LOCALID_ASAK)
    waitse
    msgbox("ASAK: Huh?!\n
        What are YOU doing here?", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(11, Common_Movement_WalkInPlaceUp)
    applymovement(12, Common_Movement_WalkInPlaceUp)
    waitmovement(0)
    msgbox("ASAK, you know this person?\p
        ASAK: … Yes.\n
        They're a… friend… of MASTER BENNIS.", MSGBOX_AUTOCLOSE)
    waitmessage
    playse(SE_PIN)
    applymovement(11, Common_Movement_ExclamationMark)
    delay(8)
    applymovement(12, Common_Movement_ExclamationMark)
    waitmovement(12)
    msgbox("I see! My deepest apologies.", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(11, Common_Movement_WalkInPlaceDown)
    applymovement(12, Common_Movement_WalkInPlaceDown)
    waitmovement(11)
    msgbox("We will allow you entry out of respect\n
        for MASTER BENNIS.\p
        But if you cause any trouble, you'll be\n
        dealt with accordingly.", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(11, Common_Movement_WalkLeft)
    delay(8)
    applymovement(12, Common_Movement_WalkRight)
    waitmovement(12)
    applymovement(11, Common_Movement_WalkInPlaceDown)
    delay(8)
    applymovement(12, Common_Movement_WalkInPlaceDown)
    waitmovement(12)
    applymovement(LOCALID_ASAK, Common_Movement_WalkUp6)
    delay(8)
    if(var(VAR_TEMP_B) == 1){
        applymovement(OBJ_EVENT_ID_PLAYER, Baldesia_Movement_PlayerEntersBaldesiaLeftTrigger)
    }
    elif(var(VAR_TEMP_B) == 2){
        applymovement(OBJ_EVENT_ID_PLAYER, Baldesia_Movement_PlayerEntersBaldesiaRightTrigger)
    }
    else{
        applymovement(OBJ_EVENT_ID_PLAYER, Baldesia_Movement_PlayerEntersBaldesia)
    }
    waitmovement(0)
    applymovement(LOCALID_ASAK, Common_Movement_WalkInPlaceDown)
    waitmovement(LOCALID_ASAK)
    msgbox("ASAK: You're lucky that MASTER BENNIS\n
        has acknowledged your strength.\p
        If it was up to me, I'd-", MSGBOX_AUTOCLOSE)
    waitmessage
    addobject(LOCALID_BENNIS)
    addobject(LOCALID_ZORUA)
    playse(SE_PIN)
    applymovement(LOCALID_ASAK, Common_Movement_ExclamationMark)
    waitmovement(LOCALID_ASAK)
    waitse
    applymovement(LOCALID_ASAK, Common_Movement_WalkInPlaceLeft)
    delay(8)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    fadenewbgm(MUS_BENNIS_DOJO)
    savebgm(MUS_BENNIS_DOJO)
    applymovement(LOCALID_BENNIS, Common_Movement_WalkRight7)
    delay(16)
    applymovement(LOCALID_ZORUA, Common_Movement_WalkRight7)
    waitmovement(0)
    msgbox(Baldesia_Text_Bennis1, MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(LOCALID_ASAK, Common_Movement_WalkInPlaceDown)
    waitmovement(0)
    msgbox("ASAK: Yes. I caught them trying to enter\n
        the village.", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(LOCALID_ASAK, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    msgbox("…By the way, who is that next\n
        to you?\p
        MASTER BENNIS: Hm? It's PANINI.", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(LOCALID_BENNIS, Common_Movement_WalkInPlaceDown)
    waitmovement(0)
    msgbox("Right, PANINI?", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(LOCALID_ZORUA, Common_Movement_WalkInPlaceUp)
    waitmovement(0)
    playse(SE_BIKE_HOP)
    applymovement(LOCALID_ZORUA, Common_Movement_QuestionMark)
    waitmovement(0)
    waitse
    msgbox("PANINI?: Paneenee?", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(LOCALID_BENNIS, Common_Movement_WalkInPlaceRight)
    delay(16)
    applymovement(LOCALID_ZORUA, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    msgbox("MASTER BENNIS: See?", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(LOCALID_ASAK, Baldesia_Movement_AsakInspectZorua1)
    waitmovement(0)
    delay(16)
    applymovement(LOCALID_ASAK, Baldesia_Movement_AsakInspectZorua2)
    delay(8)
    applymovement(LOCALID_ZORUA, Common_Movement_FaceDown)
    waitmovement(LOCALID_ASAK)
    delay(16)
    applymovement(LOCALID_ASAK, Baldesia_Movement_AsakInspectZorua3)
    delay(8)
    applymovement(LOCALID_ZORUA, Common_Movement_FaceLeft)
    waitmovement(LOCALID_ASAK)
    delay(16)
    applymovement(LOCALID_ASAK, Baldesia_Movement_AsakWalkToBennis)
    delay(8)
    applymovement(LOCALID_BENNIS, Common_Movement_WalkInPlaceLeft)
    delay(8)
    applymovement(LOCALID_ZORUA, Common_Movement_WalkInPlaceUp)
    waitmovement(LOCALID_ZORUA)
    msgbox("ASAK: Sir, I'm not sure if that's PANINI…", MSGBOX_AUTOCLOSE)
    waitmessage
    addobject(LOCALID_PANINI)
    applymovement(LOCALID_PANINI, Common_Movement_WalkRight7)
    waitmovement(0)
    applymovement(LOCALID_ASAK, Common_Movement_WalkInPlaceDown)
    delay(8)
    applymovement(LOCALID_BENNIS, Common_Movement_WalkInPlaceDown)
    applymovement(LOCALID_ZORUA, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    playse(SE_PIN)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
    applymovement(LOCALID_ASAK, Common_Movement_ExclamationMark)
    applymovement(LOCALID_BENNIS, Common_Movement_ExclamationMark)
    waitmovement(0)
    waitse
    playbgm(MUS_NONE, FALSE)
    delay(30)
    playse(SE_BIKE_HOP)
    applymovement(LOCALID_ZORUA, Common_Movement_QuestionMark)
    delay(6)
    applymovement(LOCALID_PANINI, Common_Movement_QuestionMark)
    waitmovement(0)
    waitse
    msgbox("PANINI: Panini?\n
        PANINI?: Paneenee?", MSGBOX_AUTOCLOSE)
    waitmessage
    playse(SE_SUDOWOODO_SHAKE)
    applymovement(LOCALID_PANINI, Baldesia_Movement_PaniniMirror)
    applymovement(LOCALID_ZORUA, Baldesia_Movement_PaniniMirror)
    waitmovement(0)
    msgbox("PANINI: Panini!\n
        PANINI?: Paneenee!", MSGBOX_AUTOCLOSE)
    waitmessage
    playse(SE_RG_HELP_OPEN)
    applymovement(LOCALID_PANINI, Baldesia_Movement_PaniniMirror2)
    applymovement(LOCALID_ZORUA, Baldesia_Movement_PaniniMirror2Reverse)
    waitmovement(0)
    delay(20)
    playse(SE_RG_HELP_CLOSE)
    applymovement(LOCALID_PANINI, Baldesia_Movement_PaniniMirror4)
    applymovement(LOCALID_ZORUA, Baldesia_Movement_PaniniMirror4Reverse)
    waitmovement(0)
    delay(60)
    playse(SE_BIKE_HOP)
    applymovement(LOCALID_PANINI, Baldesia_Movement_PaniniMirror3)
    applymovement(LOCALID_ZORUA, Baldesia_Movement_PaniniMirror3Reverse)
    waitmovement(0)
    msgbox("PANINI: Panini!", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(LOCALID_PANINI, Common_Movement_WalkInPlaceFasterRight)
    waitmovement(0)
    playse(SE_EFFECTIVE)
    fadescreen(FADE_TO_WHITE)
    removeobject(LOCALID_ZORUA)
    addobject(24)
    waitse
    fadescreen(FADE_FROM_WHITE)
    delay(60)
    playse(SE_PIN)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
    applymovement(LOCALID_ASAK, Common_Movement_ExclamationMark)
    delay(8)
    applymovement(LOCALID_BENNIS, Common_Movement_ExclamationMark)
    delay(8)
    applymovement(LOCALID_PANINI, Common_Movement_ExclamationMark)
    waitmovement(0)
    waitse
    delay(30)
    applymovement(24, Common_Movement_WalkInPlaceRight)
    waitmovement(24)
    playmoncry(SPECIES_ZORUA_HISUI, CRY_MODE_ENCOUNTER)
    msgbox("ZORUA: Paneenee!", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(24, Common_Movement_WalkRight)
    waitmovement(24)
    setwildbattle(SPECIES_ZORUA_HISUI, 50)
    dowildbattle
    applymovement(LOCALID_PANINI, Baldesia_Movement_PaniniFaceBennis)
    waitmovement(0)
    msgbox("PANINI: Panini?! Panini panini?!\n
        Panini!", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(LOCALID_PANINI, Baldesia_Movement_WalkFastLeft7)
    delay(24)
    applymovement(LOCALID_BENNIS, Common_Movement_WalkInPlaceLeft)
    delay(8)
    applymovement(LOCALID_ASAK, Common_Movement_WalkInPlaceLeft)
    waitmovement(LOCALID_PANINI)
    msgbox(Baldesia_Text_Bennis2, MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(LOCALID_ASAK, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    msgbox("ASAK: N-No, sir! W-Well, not in\n
    your overworld sprite, at least…", MSGBOX_AUTOCLOSE)
    waitmessage
    playbgm(MUS_NONE, FALSE)
    playse(SE_SHINY)
    fadescreen(FADE_TO_WHITE)
    fadescreen(FADE_FROM_WHITE)
    waitse
    applymovement(LOCALID_ASAK, Common_Movement_WalkInPlaceFasterDown)
    delay(4)
    applymovement(LOCALID_BENNIS, Common_Movement_WalkInPlaceFasterDown)
    delay(4)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterDown)
    waitmovement(0)
    msgbox("ASAK: Ah! My eyes! Now what?!\n
        What was that flash?!", MSGBOX_AUTOCLOSE)
    waitmessage
    playse(SE_SHINY)
    fadescreen(FADE_TO_WHITE)
    fadescreen(FADE_FROM_WHITE)
    waitse
    playse(SE_M_PETAL_DANCE)
    fadescreenspeed(FADE_TO_WHITE, 4)
    fadescreenspeed(FADE_FROM_WHITE, 3)
    waitse
    addobject(LOCALID_MOMO)
    applymovement(LOCALID_MOMO, Baldesia_Movement_MomoEnters)
    waitmovement(LOCALID_MOMO)
    playse(SE_PIN)
    applymovement(LOCALID_ASAK, Common_Movement_ExclamationMark)
    delay(8)
    applymovement(LOCALID_BENNIS, Common_Movement_ExclamationMark)
    delay(8)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    waitse
    fadedefaultbgm
    msgbox(Baldesia_Text_Bennis3, MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(LOCALID_MOMO, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    msgbox("… Hi, {PLAYER}.", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(LOCALID_BENNIS, Common_Movement_WalkInPlaceRight)
    applymovement(LOCALID_ASAK, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    msgbox(Baldesia_Text_Bennis4, MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(LOCALID_BENNIS, Common_Movement_WalkInPlaceDown)
    delay(8)
    applymovement(LOCALID_ASAK, Common_Movement_WalkInPlaceDown)
    delay(8)
    applymovement(LOCALID_MOMO, Common_Movement_WalkInPlaceUp)
    waitmovement(0)
    msgbox(Baldesia_Text_Bennis5, MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(LOCALID_MOMO, Baldesia_Movement_MomoGoesToMainHouse)
    delay(30)
    applymovement(LOCALID_BENNIS, Common_Movement_WalkInPlaceUp)
    delay(8)
    applymovement(LOCALID_ASAK, Common_Movement_WalkInPlaceUp)
    delay(8)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceUp)
    waitmovement(LOCALID_MOMO)
    playse(SE_DOOR)
    removeobject(LOCALID_MOMO)
    applymovement(LOCALID_BENNIS, Common_Movement_WalkInPlaceLeft)
    delay(8)
    applymovement(LOCALID_ASAK, Common_Movement_WalkInPlaceRight)
    delay(8)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    msgbox("MASTER BENNIS: I'm going too.", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(LOCALID_BENNIS, Common_Movement_WalkUp5)
    delay(8)
    applymovement(LOCALID_ASAK, Common_Movement_WalkInPlaceUp)
    delay(8)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceUp)
    waitmovement(LOCALID_BENNIS)
    playse(SE_DOOR)
    waitse
    applymovement(LOCALID_ASAK, Baldesia_Movement_AsakApproachesPlayer)
    delay(8)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceLeft)
    waitmovement(LOCALID_ASAK)
    msgbox("ASAK: Sorry, {PLAYER}.\n
        Could I ask you to go get PANINI and\l
        then come to the MAIN HOUSE?", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(LOCALID_ASAK, Common_Movement_WalkInPlaceUp)
    waitmovement(0)
    msgbox("MASTER BENNIS! Wait for me!", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(LOCALID_ASAK, Baldesia_Movement_AsakGoesToMainHouse)
    delay(8)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceUp)
    waitmovement(LOCALID_ASAK)
    playse(SE_DOOR)
    applymovement(LOCALID_ASAK, Common_Movement_WalkUp)
    waitmovement(0)
    waitse
    removeobject(LOCALID_ASAK)
    removeobject(LOCALID_BENNIS)
    removeobject(LOCALID_PANINI)
    setvar(VAR_MOMO_ARIA_STATE, 2)
    releaseall
    end
}

script Baldesia_EventScript_INeedToGoGetPanini{
    lockall
    msgbox("I need to go get PANINI first.", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
    waitmovement(0)
    releaseall
    end
}

script Baldesia_EventScript_YouNeedPermission{
    lockall
    playse(SE_PIN)
    applymovement(9, Common_Movement_ExclamationMark)
    waitmovement(9)
    waitse
    applymovement(9, Common_Movement_WalkInPlaceRight)
    delay(8)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    msgbox("Sorry, you need permission from the\n
        Village Chief to travel to the altar.", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
    waitmovement(0)
    applymovement(9, Common_Movement_FaceOriginalDirection)
    waitmovement(9)
    releaseall
    end
}

text Baldesia_Text_Bennis1{
    "ASAK: MASTER BENNIS!\p
    MASTER BENNIS: ASAK. Oho? And… you're\n
    here too, I see."
}

text Baldesia_Text_Bennis2{
    "MASTER BENNIS: Oh, beef.\n
    Do I really have a weird shaped butt?"
}

text Baldesia_Text_Bennis3{
    "ASAK: L-LADY ASAKURA?\p
    MASTER BENNIS: Young Lady.\n
    Your scalp is as shiny as ever.\p
    MOMO: … … … …\n
    I'm back."
}

text Baldesia_Text_Bennis4{
    "MASTER BENNIS: {PLAYER}? Hm.\p
    ASAK: Huh… I just realized we never knew\n
    your name.\l
    And you also know Lady ASAKURA…\l
    I'm surprised."
}

text Baldesia_Text_Bennis5{
    "MASTER BENNIS: We were searching all\n
    over HOENN for you.\l
    Where have you been?\p
    MOMO: … I made it back in time for the\n
    ritual… I think.\l
    I need to go see brother now."}

movement Baldesa_Movement_BlockTrainerLeftGuard{
    walk_fast_right
    walk_in_place_fast_down
}

movement Baldesa_Movement_BlockTrainerRightGuard{
    walk_fast_left
    walk_in_place_fast_down
}

movement Baldesia_Movement_PlayerEntersBaldesia{
    walk_up * 7
}

movement Baldesia_Movement_PlayerEntersBaldesiaLeftTrigger{
    walk_right
    walk_up * 7
}

movement Baldesia_Movement_PlayerEntersBaldesiaRightTrigger{
    walk_left
    walk_up * 7
}

movement Baldesia_Movement_AsakInspectZorua1{
    walk_fast_left
    walk_fast_down
    walk_in_place_fast_left
}

movement Baldesia_Movement_AsakInspectZorua2{
    walk_fast_down
    walk_fast_left
    walk_in_place_fast_up
}

movement Baldesia_Movement_AsakInspectZorua3{
    walk_fast_left
    walk_fast_up
    walk_in_place_fast_right
}

movement Baldesia_Movement_AsakWalkToBennis{
    walk_fast_up
    walk_in_place_fast_right
}

movement Baldesia_Movement_PaniniMirror{
    lock_facing_direction
    walk_fast_down * 2
    delay_16
    walk_fast_up * 2
    delay_16
    unlock_facing_direction
}

movement Baldesia_Movement_PaniniMirror2{
    walk_in_place_down
    delay_4
    walk_in_place_left
    delay_4
    walk_in_place_up
    delay_4
    walk_in_place_right
}

movement Baldesia_Movement_PaniniMirror4Reverse{
    walk_in_place_up
    delay_4
    walk_in_place_right
    delay_4
    walk_in_place_down
    delay_4
    walk_in_place_left
}

movement Baldesia_Movement_PaniniMirror3{
    jump_in_place_left_right
}

movement Baldesia_Movement_PaniniMirror3Reverse{
    jump_in_place_right_left
}

movement Baldesia_Movement_PaniniMirror4{
    walk_in_place_up
    delay_4
    walk_in_place_left
    delay_4
    walk_in_place_down
    delay_4
    walk_in_place_right
}

movement Baldesia_Movement_PaniniMirror2Reverse{
    walk_in_place_down
    delay_4
    walk_in_place_right
    delay_4
    walk_in_place_up
    delay_4
    walk_in_place_left
}

movement Baldesia_Movement_PaniniFaceBennis{
    walk_right
    walk_in_place_up
}

movement Baldesia_Movement_WalkFastLeft7{
    walk_fast_left * 7
}

movement Baldesia_Movement_MomoEnters{
    walk_up * 4
    walk_left
    walk_up * 2
}

movement Baldesia_Movement_MomoGoesToMainHouse{
    walk_right
    walk_up * 3
    walk_left
    walk_up * 3
}

movement Baldesia_Movement_AsakGoesToMainHouse{
    walk_fast_left
    walk_fast_up * 7
}

movement Baldesia_Movement_AsakApproachesPlayer{
    walk_fast_down
    walk_fast_right * 2
}
