mapscripts Route135_Lab_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_ULTRA_SPACE_STATE, 0: Route135_Lab_EventScript_ToUltraSpace
        VAR_ULTRA_SPACE_STATE, 3: Route135_Lab_EventScript_EndUltraSpace
    ]
    MAP_SCRIPT_ON_TRANSITION: Route135_Lab_OnTransition
}

script Route135_Lab_OnTransition{
    setflag(FLAG_TEMP_E)
    call_if_unset(FLAG_VISITED_ROUTE_135_LAB, Route135_SetFlag)
    call_if_ge(VAR_ULTRA_SPACE_STATE, 4, Route135_Lab_JamesPosition)
    end
}


script Route135_SetFlag{
    setflag(FLAG_VISITED_ROUTE_135_LAB)
    return
}

script Route135_Lab_JamesPosition{
    setobjectxyperm(2, 5, 2)
    setobjectmovementtype(2, MOVEMENT_TYPE_FACE_UP)
    return
}

script Route135_Lab_EventScript_Maniac{
    lock
    if(var(VAR_ULTRA_SPACE_STATE) == 4){
        msgbox(Route_135_Lab_Text_ImBusy, MSGBOX_DEFAULT)
        waitmessage
    }
    elif(var(VAR_ULTRA_SPACE_STATE) == 5){
        goto(Route135_Lab_EventScript_ToAreaZero)
    }
    elif(var(VAR_ULTRA_SPACE_STATE) == 6){
        goto(Route135_Lab_EventScript_TalkToCosmog)  
    }
    msgbox(Route_135_Lab_Text_ImBusyTalkToCosmog, MSGBOX_DEFAULT)
    waitmessage
    release
    end
}

script Route135_Lab_EventScript_James{
    lock
    faceplayer
    msgbox(Route_135_Lab_Text_ImGoingToStayHere, MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(2, Common_Movement_WalkInPlaceUp)
    waitmovement(2)
    release
    end
}

script Route135_Lab_EventScript_ToUltraSpace{
    lockall
    playse(SE_PIN)
    applymovement(1, Common_Movement_ExclamationMark)
    waitmovement(1)
    waitse
    applymovement(1, Route135_Lab_Movement_ManiacApproachPlayer)
    waitmovement(1)
    msgbox(Route135_Lab_Text_Maniac1, MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES){
        msgbox(Route135_Lab_Text_Maniac2, MSGBOX_AUTOCLOSE)
        waitmessage
        applymovement(1, Route135_Lab_Movement_ManiacGoBackToPC)
        delay(16)
        applymovement(OBJ_EVENT_ID_PLAYER, Route135_Lab_Movement_PlayerGoBackToPC)
        waitmovement(0)
        msgbox(Route135_Lab_Text_Maniac3, MSGBOX_AUTOCLOSE)
        waitmessage
        applymovement(1, Common_Movement_WalkInPlaceRight)
        waitmovement(1)
        msgbox(" In fact, this POKéMON here came from one\n
            of those rifts.\l
            I've named it COSMOG.", MSGBOX_AUTOCLOSE)
        applymovement(3, Common_Movement_WalkInPlaceLeft)
        waitmovement(3)
        playmoncry(SPECIES_COSMOG, CRY_MODE_ENCOUNTER)
        waitmoncry
        applymovement(1, Common_Movement_WalkInPlaceDown)
        waitmovement(1)
        msgbox(Route135_Lab_Text_Maniac4, MSGBOX_AUTOCLOSE)
        waitmessage
        applymovement(1, Common_Movement_WalkInPlaceUp)
        waitmovement(1)
        msgbox("Assistant!", MSGBOX_AUTOCLOSE)
        waitmessage
        delay(20)
        applymovement(2, Route135_Lab_Movement_JamesEnter)
        waitmovement(2)
        applymovement(1, Common_Movement_WalkInPlaceLeft)
        waitmovement(0)
        playse(SE_PIN)
        applymovement(2, Common_Movement_ExclamationMark)
        waitmovement(2)
        waitse
        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceLeft)
        waitmovement(0)
        msgbox(Route135_Lab_Text_James, MSGBOX_AUTOCLOSE)
        waitmessage
        applymovement(1, Common_Movement_WalkInPlaceDown)
        delay(8)
        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceUp)
        waitmovement(0)
        msgbox(Route135_Lab_Text_Maniac5, MSGBOX_AUTOCLOSE)
        waitmessage
        applymovement(1, Route135_Lab_Movement_ManiacStartExperiment)
        delay(16)
        applymovement(2, Route135_Lab_Movement_JamesStartExperiment)
        delay(16)
        applymovement(OBJ_EVENT_ID_PLAYER, Route135_Lab_Movement_PlayerStartExperiment)
        waitmovement(2)
        fadenewbgm(MUS_NONE)
        msgbox(Route135_Lab_Text_Maniac6, MSGBOX_AUTOCLOSE)
        waitmessage
        closemessage
        playse(SE_BERRY_BLENDER)
        delay(60)
        msgbox(Route135_Lab_Text_Maniac7, MSGBOX_AUTOCLOSE)
        waitmessage
        closemessage
        playse(SE_M_THUNDERBOLT2)
        waitse
        playse(SE_BREAKABLE_DOOR)
        waitse
        delay(10)
        call(Route135_Lab_Shake)
        setvar(VAR_ULTRA_SPACE_STATE, 1)
        warpwhitefade(MAP_ULTRA_SPACE, 40, 65)
        waitstate
    }
    else{
        msgbox(Route135_Lab_Text_ManiacNo, MSGBOX_AUTOCLOSE)
        waitmessage
        closemessage
        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
        waitmovement(0)
        warp(MAP_ROUTE135, 8, 10)
        waitstate
    }
    releaseall
    end
}

script Route135_Lab_Shake{
    delay (30)
    setvar(VAR_0x8004, 2) // vertical pan
    setvar(VAR_0x8005, 6) // horizontal pan
    setvar(VAR_0x8006, 8) // num shakes
    setvar(VAR_0x8007, 4) // shake delay
    special(ShakeCamera)
    waitstate
    delay(60)
    playse(SE_M_THUNDER_WAVE)
    fadescreen(FADE_TO_WHITE)
    fadescreen(FADE_FROM_WHITE)
    delay(20)
    playse(SE_M_THUNDER_WAVE)
    fadescreen(FADE_TO_WHITE)
    fadescreen(FADE_FROM_WHITE)
    delay(10)
    waitse
    playse(SE_INTRO_BLAST)
    delay(20)
    return
}

script Route135_Lab_EventScript_Cosmog{
    lock
    faceplayer
    playmoncry(SPECIES_COSMOG, CRY_MODE_ENCOUNTER)
    waitmoncry
    goto_if_ge(VAR_ULTRA_SPACE_STATE, 6, Route135_Lab_EventScript_ReturnToUltraSpaceOrAreaZero)
    release
    end
}

script Route135_Lab_EventScript_EndUltraSpace{
    lockall
    applymovement(4, Route_135_Lab_ManiacLookAround)
    delay(8)
    applymovement(OBJ_EVENT_ID_PLAYER, Route_135_Lab_PlayerLookAround)
    delay(8)
    applymovement(5, Route_135_Lab_JamesLookAround)
    waitmovement(0)
    msgbox("MANIAC: We…\n
        We really returned…", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(4, Common_Movement_WalkInPlaceUp)
    waitmovement(4)
    msgbox(Route135_Lab_Text_Maniac8, MSGBOX_AUTOCLOSE)
    waitmessage
    playmoncry(SPECIES_COSMOG, CRY_MODE_ENCOUNTER)
    waitmoncry
    applymovement(4, Route_135_Lab_Movement_ManiacReturnsToComputer)
    delay(8)
    applymovement(6, Route_135_Lab_Movement_CosmogReturnsToComputer)
    delay(8)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceRight)
    delay(8)
    applymovement(5, Common_Movement_WalkInPlaceRight)
    waitmovement(6)
    applymovement(4, Common_Movement_WalkInPlaceLeft)
    waitmovement(4)
    msgbox(Route135_Lab_Text_Maniac9, MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(4, Common_Movement_WalkInPlaceRight)
    waitmovement(4)
    applymovement(5, Route_135_Lab_Movement_JamesFacePlayer)
    waitmovement(5)
    msgbox(Route135_Lab_Text_James2, MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(5, Common_Movement_WalkInPlaceUp)
    waitmovement(5)
    msgbox("I think I understand why JIMMY gave\n
        me this job now…", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(5, Common_Movement_WalkInPlaceLeft)
    waitmovement(5)
    msgbox(Route135_Lab_Text_James3, MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(5, Route_135_Lab_Movement_JamesReturnsToComputer)
    delay(8)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceUp)
    waitmovement(5)
    applymovement(OBJ_EVENT_ID_PLAYER, Route_135_Lab_Movement_PlayerLeaves)
    waitmovement(0)
    removeobject(5)
    setflag(FLAG_HIDE_NPCS_RETURNED_FROM_ULTRA_SPACE)
    clearflag(FLAG_HIDE_PROF_ROUTE_135_LAB)
    setvar(VAR_ULTRA_SPACE_STATE, 4)
    call_if_set(FLAG_SYS_DEFEATED_RED, Route135_Lab_TryAdvanceUltraSpaceVar)
    setrespawn(HEAL_LOCATION_ROUTE135)
    warp(MAP_ROUTE135, 8, 10)
    waitstate
    releaseall
    end
}

// Insurance for if player defeats Red before doing the Ultra Space event. Defeating Red unlocks Area Zero.
script Route135_Lab_TryAdvanceUltraSpaceVar{
    setvar(VAR_ULTRA_SPACE_STATE, 5)
    return
}

script Route135_Lab_EventScript_ToAreaZero{
    lock
    faceplayer
    if(flag(FLAG_TEMP_1) == TRUE){
        msgbox("Ah, intern. Did you change your mind?", MSGBOX_YESNO)
    }
    else{
        msgbox(Route135_Lab_Text_ManiacIntroduceAreaZero, MSGBOX_YESNO)
    }
    waitmessage
    if(var(VAR_RESULT) == YES){
        msgbox("Resplendent!\n
            Go stand next to COSMOG.\p
            It shall transport you.", MSGBOX_AUTOCLOSE)
        call(Route135_Lab_EventScript_PlayerGoesToCosmog)
        waitmovement(0)
        msgbox(Route135_Lab_Text_ManiacExplainCosmog, MSGBOX_AUTOCLOSE)
        waitmessage
        applymovement(3, Common_Movement_WalkInPlaceDown)
        waitmovement(3)
        playmoncry(SPECIES_COSMOG, CRY_MODE_ENCOUNTER)
        waitmoncry
        call(Route135_Lab_Shake)
        setvar(VAR_ULTRA_SPACE_STATE, 6)
        warpwhitefade(MAP_AREA_ZERO, 15, 11)
        waitstate
    }
    else{
        msgbox("…I see. Well, you know where\n
            to find me if you change your mind.", MSGBOX_AUTOCLOSE)
        waitmessage
        applymovement(1, Common_Movement_FaceOriginalDirection)
        waitmovement(1)
        setflag(FLAG_TEMP_1)
    }
    release
    end
}

script Route135_Lab_EventScript_TalkToCosmog{
    lock
    faceplayer
    msgbox("Ah, intern.\n
        It seems the survey went well.\l
        Thanks for the great data.\p
        Well, other than that, there hasn't\n
        been any significant new developments.\p
        But on another trip to ULTRA SPACE, we\n
        found these devices.\p
        I couldn't find any use for them, but\n
        maybe you can.", MSGBOX_AUTOCLOSE)
    giveitem(ITEM_N_SOLARIZER)
    waitmessage
    giveitem(ITEM_N_LUNARIZER)
    waitmessage
    msgbox("If you'd like to return to one of the\n
        previous worlds, COSMOG is ready.\l
        It seems to trust you now." MSGBOX_AUTOCLOSE)
    applymovement(3, Common_Movement_FacePlayer)
    waitmovement(3)
    playmoncry(SPECIES_COSMOG, CRY_MODE_ENCOUNTER)
    waitmoncry
    msgbox("I'm busy, so I'll leave it to you.\n
        You know the routine by now.", MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(1, Common_Movement_FaceOriginalDirection)
    waitmovement(1)
    setvar(VAR_ULTRA_SPACE_STATE, 7)
    clearflag(FLAG_RECEIVED_MELTAN)
    release
    end
}

script Route135_Lab_EventScript_ReturnToUltraSpaceOrAreaZero{
    lock
    message("{COLOR BLUE}{SHADOW LIGHT_BLUE}Where would you like to go?")
    multichoice(25, 5, MULTI_ULTRA_SPACE_CHOICE, 0)
    closemessage
    if(var(VAR_RESULT) == 0){
        playmoncry(SPECIES_COSMOG, CRY_MODE_ENCOUNTER)
        waitmoncry
        call(Route135_Lab_Shake)
        warpwhitefade(MAP_ULTRA_SPACE, 26, 49)
        waitstate
    }
    elif(var(VAR_RESULT) == 1){
        playmoncry(SPECIES_COSMOG, CRY_MODE_ENCOUNTER)
        waitmoncry
        call(Route135_Lab_Shake)
        warpwhitefade(MAP_AREA_ZERO, 15, 11)
        waitstate
    }
    release
    end
}

script Route135_Lab_EventScript_PlayerGoesToCosmog{
    getobjectfacingdirection(OBJ_EVENT_ID_PLAYER, VAR_TEMP_0)
    if(var(VAR_TEMP_0) == DIR_EAST){
        applymovement(OBJ_EVENT_ID_PLAYER, Route135_Lab_Movement_PlayerGoesToCosmogEast)
        waitmovement(0)
    }
    else{
        applymovement(OBJ_EVENT_ID_PLAYER, Route135_Lab_Movement_PlayerGoesToCosmogNorth)
        waitmovement(0)
    }
    applymovement(1, Common_Movement_WalkInPlaceUp)
    waitmovement(1)
    return
}

movement Route135_Lab_Movement_ManiacApproachPlayer{
    walk_left * 5
    walk_down * 2
}

movement Route135_Lab_Movement_ManiacGoBackToPC{
    walk_up * 3
    walk_right * 4
    walk_in_place_down
}

movement Route135_Lab_Movement_PlayerGoBackToPC{
    walk_up * 3
    walk_right * 4
    walk_in_place_up
}

text Route135_Lab_Text_Maniac1{
    "Hm? Are you the new intern?"
}

text Route135_Lab_Text_Maniac2{
    "Resplendent!\n
    You're just in time.\l
    Come with me."
}

text Route135_Lab_Text_Maniac3{
    "I presume you're already aware of\n
    what's being researched here… but\l
    to reiterate, I'm currently looking\l
    into the possibility of traveling\l
    to new worlds.\p
    In fact, I discovered the existence of\n
    different dimensions parallel to ours.\p
    I've named the endless expanse\n
    between these worlds ULTRA SPACE.\p
    Around the world, there have been\n
    rifts across the region that connect\l
    our world to this ULTRA SPACE."
    }

text Route135_Lab_Text_Maniac4{
    "I've reached a breakthrough in my\n
    experiments and now plan to attempt\l
    to open a rift to the ULTRA SPACE.\p
    You made it just in time.\n
    Quite fortuitous, indeed."
}

movement Route135_Lab_Movement_JamesEnter{
    walk_down * 6
    walk_in_place_right
}

text Route135_Lab_Text_James{
    "JAMES: {PLAYER}?\n
    What are you doing here?"
}

text Route135_Lab_Text_Maniac5{
    "MANIAC: Ah, so you two are\n
    already acquainted.\p
    Then let us forgo the formalities and\n
    get right to the experiment!"
}

movement Route135_Lab_Movement_ManiacStartExperiment{
    walk_up * 3
    walk_right
    walk_in_place_up
}

movement Route135_Lab_Movement_JamesStartExperiment{
    walk_up * 3
    walk_right
}

movement Route135_Lab_Movement_PlayerStartExperiment{
    walk_up * 2
}

movement Route_135_Lab_ManiacLookAround{
    walk_in_place_down
    delay_8
    walk_in_place_left
    delay_8
    walk_in_place_up
    delay_8
    walk_in_place_right

}

movement Route_135_Lab_JamesLookAround{
    walk_in_place_up
    delay_8
    walk_in_place_right
    delay_8
    walk_in_place_down
    delay_8
    walk_in_place_left

}

movement Route_135_Lab_PlayerLookAround{
    walk_in_place_right
    delay_8
    walk_in_place_down
    delay_8
    walk_in_place_up
    delay_8
    walk_in_place_left

}

text Route135_Lab_Text_Maniac6{
    "This machine I've built should be\n
    capable of creating the rift…\l
    Let us begin."
}

text Route135_Lab_Text_Maniac7{
    "It's working! It's really working!\n
    RESPLENDENT!\p
    I knew I was a genius!\n
    Wait… No! Something's wrong!"
}

text Route135_Lab_Text_ManiacNo{
    "I see…\n
    Well, this is a private LAB.\p
    If you're not going to help me with\n
    my research, you'll have to leave."
}

text Route135_Lab_Text_Maniac8{
    "Great work COSMOG!\n
    These discoveries are monumental!\p
    I must log all my findings this instant!\n
    Come with me, COSMOG!"
}

text Route135_Lab_Text_Maniac9{
    "MANIAC: Thanks for your work, intern.\n
    You're free to do as you would.\p
    I shall contact you if your assistance\n
    is required again."
}

text Route135_Lab_Text_James2{
    "JAMES: …He's gonna be like that for a\n
    while.\p
    Well, either way, that really was a crazy\n
    experience.\p
    What are you going to do now, {PLAYER}?\n
    I'm going to stay and help out more.\l
    I think if I do, I'll find the answers\l
    I'm looking for."
}

text Route135_Lab_Text_James3{
    "Anyways, like the PROF. said, we'll let\n
    let you know if anything else happens.\p
    See ya, {PLAYER}."
}

text Route_135_Lab_Text_ImGoingToStayHere{
    "I don't know how long I'm going to\n
    stay here but I don't mind it.\p
    Yeah, it's in a rural area, but the people\n
    are nice.\p
    The PROF.'s a little weird, but I can\n
    tolerate it."
}


movement Route_135_Lab_Movement_ManiacReturnsToComputer{
    walk_fast_down
    walk_fast_right * 4
    walk_fast_up
    walk_fast_right
}

movement Route_135_Lab_Movement_CosmogReturnsToComputer{
    walk_up
    walk_right * 3
    walk_down
    walk_right * 2
    walk_in_place_down
}

movement Route_135_Lab_Movement_JamesFacePlayer{
    walk_up
    walk_in_place_left
}

movement Route_135_Lab_Movement_JamesReturnsToComputer{
    walk_up * 4
    walk_left * 2
    walk_up * 2
    walk_left
    walk_up * 2
}

movement Route_135_Lab_Movement_PlayerLeaves{
    walk_down * 4
}

text Route_135_Lab_Text_ImBusy{
    "Salutations, intern.\n
    There's no new breakthroughs yet.\p
    I'm busy now, so come back later."
}

movement Route135_Lab_Movement_PlayerGoesToCosmogEast{
    walk_up
    walk_in_place_down
}

movement Route135_Lab_Movement_PlayerGoesToCosmogNorth{
    walk_left
    walk_up * 2
    walk_in_place_down
}

text Route135_Lab_Text_ManiacIntroduceAreaZero{
    "Ah, intern.\n
    Salutations.\p
    I assume JAMES provided you with the\n
    relevant information, yes?\p
    As you saw, COSMOG is capable of\n
    traversing the ULTRA SPACE at will.\p
    With extensive testing and research,\n
    we've been able to survey the ULTRA\l
    SPACE further.\p
    During these surveys, we came across\n
    another rift that seems to lead to\l
    another world…\p
    Seeing as you were crazy enough to\n
    walk across the sky, I presume you'd\l
    be open to being the first human to\l
    explore this new world, yes?"
}

text Route135_Lab_Text_ManiacExplainCosmog{
    "The camera I put on COSMOG showed\n
    the world has a somewhat strange\l
    composition… Although there are signs\l
    that it was once inhabited by humans,\l
    so it should be safe to traverse.\p
    COSMOG will be with you, so if you want\n
    to return to the lab, just ask it.\p
    Now, it is time! Godspeed, intern!\n
    COSMOG, if you will!"
}

text Route_135_Lab_Text_ImBusyTalkToCosmog{
    "Ah, intern.\n
    Unfortunately, there's no new discoveries.\p
    Just ask COSMOG if you need to return to\n
    the ULTRA SPACE.\p
    It seems to trust you enough without needing\n
    my intervention any further."
}

script Route_135_Lab_EventScript_Meltan{
    lockall
    showmonpic(SPECIES_MELTAN, 10,4)
    msgbox("{PLAYER} checked the POKé BALL.\p
    	It contained the POKéMON\n
    	MELTAN.\p
    	Take the POKé BALL?", MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES){
        givemon(SPECIES_MELTAN, 70)
        if(var(VAR_RESULT) == MON_GIVEN_TO_PARTY){
            playfanfare(MUS_OBTAIN_ITEM)
            msgbox("{PLAYER} took the POKéMON!")
            removeobject(VAR_LAST_TALKED)
            setflag(FLAG_RECEIVED_MELTAN)
            call(Common_EventScript_NicknameRecievedMon)
        }
        elif(var(VAR_RESULT) == MON_GIVEN_TO_PC){
            playfanfare(MUS_OBTAIN_ITEM)
            msgbox("{PLAYER} took the POKéMON!")
            removeobject(VAR_LAST_TALKED)
            setflag(FLAG_RECEIVED_MELTAN)
            call(Common_EventScript_NicknameRecievedMonBox)
        }
        else{
            hidemonpic
            msgbox("You don't have any room.")
        }
    }
    else{
        hidemonpic
        msgbox("{PLAYER} decided not to take the \n
    	POKéMON.")
    }
    waitmessage
    releaseall
    end
}

script Route_135_Lab_EventScript_TypeNull{
    lockall
    showmonpic(SPECIES_TYPE_NULL, 10,4)
    msgbox("{PLAYER} checked the POKé BALL.\p
    	It contained the POKéMON\n
    	TYPE NULL.\p
    	Take the POKé BALL?", MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES){
        givemon(SPECIES_TYPE_NULL, 70)
        if(var(VAR_RESULT) == MON_GIVEN_TO_PARTY){
            playfanfare(MUS_OBTAIN_ITEM)
            msgbox("{PLAYER} took the POKéMON!")
            removeobject(VAR_LAST_TALKED)
            setflag(FLAG_RECEIVED_TYPE_NULL)
            call(Common_EventScript_NicknameRecievedMon)
        }
        elif(var(VAR_RESULT) == MON_GIVEN_TO_PC){
            playfanfare(MUS_OBTAIN_ITEM)
            msgbox("{PLAYER} took the POKéMON!")
            removeobject(VAR_LAST_TALKED)
            setflag(FLAG_RECEIVED_TYPE_NULL)
            call(Common_EventScript_NicknameRecievedMonBox)
        }
        else{
            hidemonpic
            msgbox("You don't have any room.")
        }
    }
    else{
        hidemonpic
        msgbox("{PLAYER} decided not to take the \n
    	POKéMON.")
    }
    waitmessage
    releaseall
    end
}
